// Disable the test report for the individual test task
subprojects {
    subprojects { p ->
        p.plugins.withType(JavaPlugin) {
            test {
                reports.html.enabled = false
            }
        }
    }    
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    subprojects { p ->
        p.plugins.withType(JavaPlugin) {
            reportOn test
        }
    }
}

allprojects { p ->
    apply plugin: 'jacoco'

    p.plugins.withType(JavaPlugin) {
        jacoco {
            toolVersion = "0.7.9"
            reportsDir = file("$buildDir/reports/jacoco")
        }

        test {
            jacoco {
                append = false
                destinationFile = file("$buildDir/jacoco/test.exec")
                classDumpDir = file("$buildDir/jacoco/classpathdumps")
                excludes = ['org/drools/**']
            }

            testLogging {
                events "failed", "standardOut", "standardError"
            }

            testLogging.showStandardStreams = true

            finalizedBy testReport
        }        

        jacocoTestReport {
            dependsOn test
            executionData fileTree(project.rootDir.absolutePath).include("/build/jacoco/test.exec")

            reports {
                html.enabled = true
                html.destination = file("${buildDir}/reports/jacoco/test")
                xml.enabled = false
                csv.enabled = false
            }
        }

        jacocoTestCoverageVerification {
            violationRules {
                rule {
                    element = 'CLASS'
                    limit {
                        minimum = 0.9
                    }
                    excludes = [
                        "com.czequered.promocodes.config.*"
                    ]
                }
            }
        }

        check.dependsOn jacocoTestReport
    }
}
